# This file is part of the Zephir.
#
# (c) Phalcon Team <team@zephir-lang.com>
#
# For the full copyright and license information, please view
# the LICENSE file that was distributed with this source code.

if(NOT DEFINED ZEPHIR_VERSION_MAJOR)
  set(ZEPHIR_VERSION_MAJOR 1)
endif()

if(NOT DEFINED ZEPHIR_VERSION_MINOR)
  set(ZEPHIR_VERSION_MINOR 0)
endif()

if(NOT DEFINED ZEPHIR_VERSION_PATCH)
  set(ZEPHIR_VERSION_PATCH 0)
endif()

if(NOT DEFINED ZEPHIR_VERSION_SUFFIX)
  set(ZEPHIR_VERSION_SUFFIX source)
endif()

set(PROJECT_VERSION_WITHOUT_TWEAK
    ${ZEPHIR_VERSION_MAJOR}.${ZEPHIR_VERSION_MINOR}.${ZEPHIR_VERSION_PATCH})

if(DEFINED ZEPHIR_VERSION_TWEAK AND ZEPHIR_VERSION_TWEAK)
  set(ZEPHIR_VERSION ${PROJECT_VERSION_WITHOUT_TWEAK}.${ZEPHIR_VERSION_TWEAK})
else()
  set(ZEPHIR_VERSION ${PROJECT_VERSION_WITHOUT_TWEAK})
endif()

set(ZEPHIR_PACKAGE_STRING "${ZEPHIR_VERSION}-${ZEPHIR_VERSION_SUFFIX}")
set(ZEPHIR_PACKAGE_URL "https://zephir-lang.com")

math(
  EXPR
  ZEPHIR_VERSION_ID
  ${ZEPHIR_VERSION_MAJOR}*10000+${ZEPHIR_VERSION_MINOR}*100+${ZEPHIR_VERSION_PATCH}
)

set(ZEPHIR_PACKAGE_NAME Zephir)
set(ZEPHIR_VERSION_FULL "${ZEPHIR_PACKAGE_NAME} ${ZEPHIR_PACKAGE_STRING}")

string(TIMESTAMP CURRENT_YEAR %Y)
string(TIMESTAMP CURRENT_DATE_TIME "%Y-%m-%d %H:%M:%S")

set(ZEPHIR_TEAM "Phalcon Team")
set(ZEPHIR_COPYRIGHT "(c) 2013-${CURRENT_YEAR} ${ZEPHIR_TEAM}")
set(ZEPHIR_PACKAGE_BUILD_DATE "${CURRENT_DATE_TIME}")

set(PROJECT_VERSION ${ZEPHIR_VERSION})
set(PROJECT_DESCRIPTION "Yet another implementation of the Zephir compiler")

get_filename_component(_MOD_DIR ${CMAKE_CURRENT_LIST_FILE} PATH)
configure_file("${_MOD_DIR}/Version.hpp.in"
               "${zephir_SOURCE_DIR}/include/zephir/Version.hpp")

unset(CURRENT_YEAR)
unset(CURRENT_DATE_TIME)
unset(PROJECT_VERSION_WITHOUT_TWEAK)
